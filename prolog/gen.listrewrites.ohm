Prolog {
  Program = Sexpr+
  Sexpr =
    | "(" Sexpr* ")" -- list
    | "'" Sexpr -- quoted
    | string -- string
    | number -- number
    | Symbol -- symbol

  separator = "(" | ")" | "'" | "," | "@" | "\x60" | ";" | " . "
  vcomma = "â‚“"
  Symbol = symchar+ vcomma?
  symchar = ~vcomma ~separator ~space any
  
  string = dq (~dq any)* dq
  dq = "\""
  number = digit+ vcomma?
  semiColonComment = ";" (~"\n" any)* "\n"
  space += semiColonComment
}


ListRewrites <: Prolog {

  Program := DefineSexpr+ MainBody

  Sexpr += DefineSexpr | ControlFlowSexpr | OperationSexpr
  StatementSexpr = DefineSexpr | ControlFlowSexpr | StatementOperationSexpr | ControlFlowAtom

  DefineSexpr = 
    | "(" sym<"define"> "(" Symbol Formals ")" Body ")" -- define
    | "(" sym<"define"> Symbol Sexpr ")" -- definevar

  ControlFlowSexpr = 
    | "(" sym<"cond"> CondClauses ")" -- cond
    | "(" sym<"let"> "(" Binding ")" Body ")"-- let
    | "(" sym<"if"> Test Then Else ")"-- if

  OperationSexpr =
    | "(" sym<"eqv?"> Sexpr Sexpr ")" -- eqv
    | "(" sym<"string=?"> Sexpr Sexpr ")" -- stringeq
    | "(" sym<"eq?"> Sexpr Sexpr ")" -- eq
    | "(" sym<"+"> Sexpr Sexpr ")" -- add
    | "(" sym<"and"> Sexpr Sexpr ")" -- and
    | "(" sym<"not"> Sexpr ")" -- not

    | "(" Operator Operand* ")" -- operator
  StatementOperationSexpr = OperationSexpr

  Binding = "(" Symbol Sexpr ")" Binding?
  Test = Sexpr
  Then = StatementSexpr
  Else = StatementSexpr

  Body = StatementSexpr Body?
  MainBody = Body
  
  CondClauses = "(" CondTest CondConsequent ")" RemainingCondClauses?
  CondTest = Sexpr
  CondConsequent = Body
  RemainingCondClauses =
    | "(" sym<"else"> CondConsequent ")" -- else
    | CondClauses -- more

  Formals = Formal*
  Formal = Symbol

  Operator = Sexpr
  Operand = Sexpr
  
  ControlFlowAtom =     
    | string -- string
    | number -- number
    | Symbol -- symbol

  sym<s> = s vcomma?
}
